# Install required development tools
setup:
    @echo "Checking Rust installation..."
    @which rustc > /dev/null || { echo "ERROR: Rust not installed. Install from https://rustup.rs"; exit 1; }
    @echo "Installing required cargo tools..."
    cargo install cargo-edit --quiet 2>/dev/null || true
    cargo install cargo-deny --quiet 2>/dev/null || true
    cargo install cargo-insta --quiet 2>/dev/null || true
    cargo install cargo-llvm-cov --quiet 2>/dev/null || true
    cargo install prek --quiet 2>/dev/null || true
    cargo install --locked cocogitto --quiet 2>/dev/null || true
    @echo "Setup complete! Run 'just --list' to see available commands."

# Check code formatting (use in CI)
fmt-check:
    cargo fmt -- --check

# Auto-fix formatting
fmt:
    cargo fmt

# Run clippy with workspace lints (configured in Cargo.toml)
clippy:
    cargo clippy

# Ultra-strict clippy for CI (deny all warnings, lint all targets)
clippy-strict:
    cargo clippy --all-targets --all-features -- -D warnings

# Verify Rust toolchain pins stay synchronized
rust-version-check:
    sh scripts/check-rust-toolchain.sh

# Regenerate task indexes from task file metadata
task-index:
    python3 .agents/scripts/generate-task-indexes.py

# Verify generated task indexes are current
task-index-check:
    python3 .agents/scripts/generate-task-indexes.py --check

# Mark a task as Completed, move active/<id>.md to completed/, and refresh indexes
task-complete id:
    python3 .agents/scripts/complete-task.py {{id}}
    @just task-index

# Clippy against the Linux target so local macOS checks cover CI-only cfg paths
clippy-linux:
    @rustup target list --installed | grep -qx 'x86_64-unknown-linux-gnu' || { echo "ERROR: missing Rust target x86_64-unknown-linux-gnu. Run: rustup target add x86_64-unknown-linux-gnu"; exit 1; }
    @which x86_64-linux-gnu-gcc > /dev/null || { echo "ERROR: missing x86_64-linux-gnu-gcc cross compiler required by aws-lc-sys"; exit 1; }
    cargo clippy --target x86_64-unknown-linux-gnu --all-targets --all-features -- -D warnings

# Run tests
test:
    cargo test --quiet

# Run insta snapshot tests (requires cargo-insta; installed by `just setup`)
snapshots:
    cargo insta test

# Lint for use of super::/self:: in production code (test modules use super::* is allowed)
lint-imports:
    @grep -rn 'use super::' src/ --include='*.rs' | grep -v 'use super::\*;' | { if grep -q .; then echo "ERROR: Use crate:: paths, not super:: in production code. Found:"; grep -rn 'use super::' src/ --include='*.rs' | grep -v 'use super::\*;'; exit 1; fi; }
    @! grep -rn 'use self::' src/ --include='*.rs' | grep -q . || true
    @echo "Import lint passed!"

# Run all checks (use in CI)
ci: task-index-check rust-version-check fmt-check clippy-strict test lint-imports
    echo "All checks passed!"

# Recreate full CI pipeline locally (matches GitHub Actions)
ci-full: task-index-check fmt-check clippy-strict test lint-imports deny doc build
    echo "Full CI pipeline passed!"

# Check for denied/advisory dependencies (requires cargo-deny)
deny:
    cargo deny check advisories

# Build documentation with warnings denied
doc:
    RUSTDOCFLAGS="-D warnings" cargo doc --no-deps --document-private-items

# Run tests with coverage (requires cargo-llvm-cov)
coverage:
    cargo llvm-cov --html

# Run coverage and open report
coverage-open:
    cargo llvm-cov --html --open

# Generate coverage in lcov format for CI
coverage-lcov:
    cargo llvm-cov --lcov --output-path lcov.info

update-dependencies:
    cargo upgrade -i allow && cargo update    

build:
    cargo build --release

install:
    cargo build --release
    mkdir -p ~/bin
    cp target/release/cake ~/bin/cake
